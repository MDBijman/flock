module normalize

imports

  analysis
  signatures/flock/-
  
  codegen/util

strategies

  normalize-module =
    topdown(try(rename-var))
    //
  ; topdown(try(disambiguate-empty-literal))
  ; topdown(try(normalize-union))
    //
  ; topdown(try(result-expression-in-binding))
  ; topdown(try(hoist-in-where))
  ; topdown(try(annotate-prop-rule))
  ; bottomup(try(annotate-implicit-lattice-conversion))
  ; topdown(try(annotate-expr-term))
  ; topdown(try(annotate-binding))
  ; topdown(try(normalize-lattice-methods))
  ; fix-module-name

strategies

  rename-var: Ref(n) -> Ref(<concat-strings> ["usr", n])
  rename-var: TypedRef(n, t) -> TypedRef(<concat-strings> ["usr", n], t)
  rename-var: Var(n) -> Var(<concat-strings> ["usr", n])
  rename-var: Binding(n, e) -> Binding(<concat-strings> ["usr", n], e)
  rename-var: TypedBinding(n, t, e) -> TypedBinding(<concat-strings> ["usr", n], t, e)
  rename-var: PatternVar(n) -> PatternVar(<concat-strings> ["usr", n])
  rename-var: Param(a) -> Param(<concat-strings> ["usr", a])
  rename-var: TypedParam(a, t) -> TypedParam(<concat-strings> ["usr", a], t)

strategies

  disambiguate-empty-literal: e@EmptyLiteral(){a*} -> TypedEmptyLiteral(t){a*}
    with
      t := <get-expected-flock-type; (get-internal-map-type + get-internal-set-type + say-unknown-type)> e
  
  get-internal-map-type: UserTypeParams("SimpleMap", [a, b]) -> Map(a, b)
  
  get-internal-set-type: UserTypeParams("MustSet", [t]) -> Set(t)
  get-internal-set-type: UserTypeParams("MaySet",  [t]) -> Set(t)
  
  say-unknown-type = !("unknown if map or set: ", <id>); debug; fail
  
  // TODO: Add variants for types inside lattice
  
strategies

  normalize-union: u@Union(a, b){a*} -> TypedUnion(newA, t, newB){a*}
  with
    t := <get-flock-type> u
  ; newA := <cast-collection-literal(|a*)> (t, a) 
  ; newB := <cast-collection-literal(|a*)> (t, b)
  
  cast-collection-literal(|a*): (Set(t), SetLiteral(elems)) -> SetLiteral(newElems){a*}
  with
    newElems := <map(!Cast(<id>, t))> elems
    
  cast-collection-literal(|a*): (Map(kT, vT), MapLiteral(bindings)) -> MapLiteral(newBindings){a*}
  with
    newBindings := <map(?MapBinding(k, v); !MapBinding(<cast-if-necessary(|kT)> k, <cast-if-necessary(|vT)> v))> bindings

  cast-collection-literal(|a*): (Map(kT, vT), m@MapComp(MapBinding(k, v), s)) -> MapComp(MapBinding(newK, newV), s){a*}
  where
    newV := <cast-if-necessary(|vT)> v
  ; newK := <cast-if-necessary(|kT)> k
    
  cast-collection-literal(|a*): (t, s@SetComp(_, _)) -> <fail> "Not implemented"
  cast-collection-literal(|a*): (t, expr) -> expr
  
  cast-if-necessary(|desiredT): value -> newValue
  where
   newValue := <!(desiredT, <get-flock-type> value); not(eq) < !Cast(value, desiredT) + !value> 

  cast-if-necessary: value -> <cast-if-necessary(|expected)> value
  where
   expected := <get-expected-flock-type> value
  

strategies

  result-expression-in-binding: PropRule(_, _, ExprExprRef(Ref(_)), _) -> <id>
  result-expression-in-binding: PropRule(name, match, expr{t*}, whereClause) ->
    PropRule(name, match, ExprExprRef(Ref(newName)){t*}, newClauses)
  with 
    newName := <newname> "tmp"
  ; type := <get-flock-type> expr
  ; newBinding := TypedBinding(newName, type, expr{t*})
  ; newClauses := <append-to-where-clauses(|newBinding)> whereClause

strategies 
  
  hoist-in-where: NoWhereClause() -> <id>
  hoist-in-where: WhereClause(bindings) -> WhereClause(<map(hoist-in-where-inner); concat> bindings)
  hoist-in-where-inner = !(<id>, []); thread-bottomup_(hoist-into-binding); ?(t, bs); ![bs, [t]]; concat

  should-hoist = ?SetComp(_, _) + ?SetLiteral(_) + ?MapComp(_, _) + ?MapLiteral(_) + ?Union(_, _) + ?TypedUnion(_, _, _) + ?EmptyLiteral()
  
  /*
  * We hoist bindings and attach an annotation here
  * So that the compiler can mutate the variable with e.g. unionInplace
  * To avoid copying into a new collection
  * Since the variable is single-use
  */
  hoist-into-binding: (expr{t*}, bindings) -> (ExprExprRef(Ref(newName)), <concat> [bindings, [newBinding]])
  where
    <should-hoist> expr
  with
    newName := <newname> "tmp"
  ; type := <get-flock-type> expr{t*}
  ; newBinding := TypedBinding(newName, type, expr{t*})
  
  hoist-into-binding: (expr, bindings) -> <id> where <not(should-hoist)> expr

strategies
  
  annotate-prop-rule: pr@PropRule(name, match, expr, whereClause) -> PropRule(name, match, castExpr, whereClause)
  with
    propType := <get-flock-type> name
  ; exprType := <get-flock-type> expr
  where
   <not(eq)> (propType, exprType)
  with
    castExpr := Cast(expr, propType)

strategies
  
  annotate-implicit-lattice-conversion: Union(lhs, rhs){a*} -> <cast-if-necessary> Union(newLhs, newRhs){a*}
  where
    newLhs := <try(get-lattice-inner)> lhs 
  ; newRhs := <try(get-lattice-inner)> rhs 

  annotate-implicit-lattice-conversion: TypedUnion(lhs, t, rhs){a*} -> <cast-if-necessary> TypedUnion(newLhs,t, newRhs){a*}
  where
    newLhs := <try(get-lattice-inner)> lhs 
  ; newRhs := <try(get-lattice-inner)> rhs 

  annotate-implicit-lattice-conversion: Difference(lhs, rhs){a*} -> <cast-if-necessary> Difference(newLhs, newRhs){a*}
  where
    newLhs := <try(get-lattice-inner)> lhs 
  ; newRhs := <try(get-lattice-inner)> rhs 

strategies

  annotate-expr-term: e@ExprTerm(name, params){t*} -> TypedExprTerm(exprType, name, params){t*}
  where
    exprType := <get-flock-type> e

strategies

  annotate-binding: Binding(n, e) -> TypedBinding(n, t, e)
  where
    t := <get-flock-type> e

strategies

  normalize-lattice-methods: LatticeDefNoParams(name, kind, parts) -> LatticeDefNoParams(name, kind, newParts)
  with
    latticeType := <get-flock-type> name
  ; newParts := <map(try(lattice-result-in-let(|latticeType)))> parts

  lattice-result-in-let(|latticeType): Top(expr) -> Top(Let(newBinding, Cast(ExprExprRef(Ref(newName)), latticeType)))
  with
    newName := <newname> "tmp"
  ; innerType := <get-flock-type> expr
  ; newBinding := TypedBinding(newName, innerType, expr)  

  lattice-result-in-let(|latticeType): Bottom(expr) -> Bottom(Let(newBinding, Cast(ExprExprRef(Ref(newName)), latticeType)))
  with
    newName := <newname> "tmp"
  ; innerType := <get-flock-type> expr
  ; newBinding := TypedBinding(newName, innerType, expr)  

  lattice-result-in-let(|latticeType): Lub(p1, p2, expr) -> Lub(p1, p2, Let(newBinding, Cast(ExprExprRef(Ref(newName)), latticeType)))
  with
    newName := <newname> "tmp"
  ; innerType := <get-flock-type> expr
  ; newBinding := TypedBinding(newName, innerType, expr)
  
strategies

  fix-module-name: Module(n, s) -> Module(<concat-strings> ["norm/", n], s)

strategies

  append-to-where-clauses(|clause): NoWhereClause() -> WhereClause([clause])
  append-to-where-clauses(|clause): WhereClause(clauses) -> WhereClause(<concat> [clauses, [clause]])


