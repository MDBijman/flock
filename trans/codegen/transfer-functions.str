module codegen/transfer-functions

imports
  codegen/nodes
  codegen/util
  codegen/lattices
  codegen/expressions
  codegen/cfg-matcher
  codegen/types
  
  signatures/flock/-
  signatures/flock/desugar/-
  
  signatures/java/types/-
  signatures/java/classes/-
  signatures/java/expressions/-
  signatures/java/literals/-
  signatures/java/lexical/-
  signatures/java/names/-
  signatures/java/interfaces/-
  signatures/java/statements/-
  signatures/java/packages/-

rules
  transfer-type-name = !"TransferFunction"
  transfer-type-id = !Id(<transfer-type-name>)
  transfer-type = !ClassType(<transfer-type-id>, None())
  to-transfer-name = append-string(|<transfer-type-name>)
  
  transfer-classes(|r) = <filter(?PropRule(_, _, _, _));reverse;number(id);create-all-transfer-classes> r
  create-all-transfer-classes: r -> <concat> [<transfer-instances-class> r, <create-transfer-classes> r]
  
  set-term-to-node-term = ![LocVarDeclStm(LocalVarDecl([], ClassType(Id("IStrategoTerm"), None()), [ VariableDeclInit(VariableDecl(Id("term")), ExpressionName(AmbiguousName(Id("node")), Id("term")))]))]
  create-transfer-classes = map(create-transfer-class)
  create-transfer-class: (PropRule(prop, pat, expr, wc), index) -> java:type-dec |[
	class ~name extends TransferFunction {
		@Override
		public FlockLattice eval(Node node) {
			~bstm*:statements
		}
	}
  ]|
  where
    name := <get-transfer-id> index
  ; pattern-vars := <pattern-to-vars> pat
  ; where-vars := <where-to-code> wc
  ; statements := <concat> [
  	  <set-term-to-node-term>,
  	  pattern-vars,
  	  where-vars,
  	  <expr-to-stmts> expr,
  	  [Return(<transform-expr> expr)]
  	]
  
  pattern-to-vars: Backward(p, name) -> <concat> [<name-to-node> name, <pattern-to-decls(|"term", [])> p]
  pattern-to-vars: Forward(name, p) -> <concat> [<name-to-node> name, <pattern-to-decls(|"term", [])> p]
  pattern-to-vars: PropPatternStart(p) -> <pattern-to-decls(|[], "term")> p
  pattern-to-vars: PropPatternEnd(p) -> <pattern-to-decls(|[], "term")> p
  
  node-type-name = !"Node"
  node-type-id = !Id(<node-type-name>)
  node-type = !ClassType(<node-type-id>, None())
  name-to-node: name -> [LocVarDeclStm(LocalVarDecl([], <node-type>, [VariableDeclInit(VariableDecl(Id(<to-term-name> name)), ExpressionName(Id("node")))]))]
  
  where-to-code: NoWhereClause() -> []
  where-to-code: WhereClause(bindings) -> <map(binding-to-code); concat> bindings
  binding-to-code: Binding(var, expr) -> <fail> with <debug> "Bindings must be typed"

  binding-to-code: t@TypedBinding(var, type, expr) -> [
      stmt*,
      java:bstm |[ ~java-type ~name = ~result-expr; ]|
    ]
    where 
      <requires-statements> expr
    with
      (result-name, stmt*) := <expr-to-stmts> expr
    ; java-type := <type-to-java> type
    ; name := <to-term-name> var
    ; result-expr := <construct-from(|type)> result-name

    
  binding-to-code: TypedBinding(var, type, expr) -> [java:bstm |[ ~java-type ~name = ~java-expr; ]|]
    with
      java-type := <type-to-java> type
    ; name := <to-term-name> var
    ; java-expr := <transform-expr; simple-cast(|type)> expr
  
  transfer-instances-class: r -> [ClassDeclaration(
      []
    , Id("TransferFunctions")
    , None()
    , None()
    , None()
    , <map(create-transfer-field)> r
    )]
  create-transfer-field: (_, index) -> FieldDecl([Public(), Static()], ClassType(Id("TransferFunction"), None()), [ VariableDeclInit(VariableDecl(<get-transfer-id> index), NewInstance(None(), [], <get-transfer-id> index, [], None(), []))])
  
  get-transfer-id: index -> <concat-strings> ["TransferFunction", <int-to-string> index]
  get-transfer-function: index -> ExpressionName(AmbiguousName(Id("TransferFunctions")), <get-transfer-id> index)