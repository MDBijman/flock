module flock/Property

imports

flock/Common
flock/Pattern
flock/Type
flock/Expr
flock/Flow

context-free syntax // Properties

PropDef.PropDef = [[PropName]: [Type]]

context-free syntax // Property Rules

PropRule.PropRule = [[PropName]([PropPattern]) = [Expr] [PropWhere]]

PropPattern.Backward = [[PropCFGNodePattern] -> [Var]]
PropPattern.Forward = [[Var] -> [PropCFGNodePattern]]

PropPattern.PropPatternAmbiguity = [[Var] -> [Var]]

PropCFGNodePattern = Pattern
PropCFGNodePattern.Entry = [[Pattern].entry]
PropCFGNodePattern.Exit = [[Pattern].exit]

PropPattern.PropPatternStart = [[Pattern].start]
PropPattern.PropPatternEnd = [[Pattern].end]

PropWhere.NoWhereClause =
PropWhere.WhereClause = [where [{Binding "\n"}+]]

Binding.Binding = [[Var] = [Expr]]

context-free priorities
PropCFGNodePattern = Pattern <0> .> Pattern.PatternVar

template options

  keyword -/- [A-Za-z\-]
